DROP TABLE IF EXISTS `ufo_counties`;
DROP TABLE IF EXISTS `hbase_ufo_county`;
DROP TABLE IF EXISTS `hbase_ufo_county_shapes`;
DROP TABLE IF EXISTS `staging_wheater_data`;
DROP TABLE IF EXISTS `staging_wheater_data_aggr`;
DROP TABLE IF EXISTS `staging_ufo_count_ca`;
DROP TABLE IF EXISTS `hbase_ufos_wheater`;

CREATE EXTERNAL TABLE ufo_counties(City String, Date String, State String, Shape String, Duration String, Description String, Posted String, County String, CountyCode String) 
ROW FORMAT DELIMITED FIELDS TERMINATED BY '\073' 
STORED AS TEXTFILE 
LOCATION '/user/cloudera/ufooutput/results';


CREATE TABLE hbase_ufo_county(key string, county String, countycode String, sum_ufos int) 
STORED BY 'org.apache.hadoop.hive.hbase.HBaseStorageHandler'
WITH SERDEPROPERTIES ("hbase.columns.mapping" = ":key,a:county,a:countycode,a:sum_ufos")
TBLPROPERTIES ("hbase.table.name" = "hbase_ufo_county");


INSERT OVERWRITE TABLE hbase_ufo_county 
select county as key, county, countycode, count(city) anz
from ufo_counties 
group by county, countycode
having county != ""
and countycode !="";


CREATE TABLE hbase_ufo_county_shapes(key string, county String, countycode String, shape String, sum_ufos int) 
STORED BY 'org.apache.hadoop.hive.hbase.HBaseStorageHandler'
WITH SERDEPROPERTIES ("hbase.columns.mapping" = ":key,a:county,a:countycode,a:shape,a:sum_ufos")
TBLPROPERTIES ("hbase.table.name" = "hbase_ufo_county_shapes");

INSERT OVERWRITE TABLE hbase_ufo_county_shapes 
select county as key, county, countycode, shape, count(city) anz
from ufo_counties 
group by county, countycode, shape
having county != ""
and countycode !=""
and shape !="";


create table staging_wheater_data as select cast(concat(substring(date,0,4),"-", substring(date,5,2),"-", substring(date,7,2)) as date) as date_format, station, air_max, air_min, solar from weather_by_station;

create table staging_wheater_data_aggr as select date_format, avg((air_max+air_min)/2) avg_tmp, avg(solar) avg_solar from staging_wheater_data
group by date_format;

create table staging_ufo_count_ca as select count(*) as anz, date from ufo_counties group by date;

CREATE TABLE hbase_ufos_wheater(key string, jahr int, monat int, anz int, avg_tmp int, avg_solar int) 
STORED BY 'org.apache.hadoop.hive.hbase.HBaseStorageHandler'
WITH SERDEPROPERTIES ("hbase.columns.mapping" = ":key,a:jahr,a:monat,a:anz,a:avg_tmp,a:avg_solar")
TBLPROPERTIES ("hbase.table.name" = "hbase_ufos_wheater");

INSERT OVERWRITE TABLE hbase_ufos_wheater 
select date, year(date) jahr, month(date) monat, anz, cast(avg_tmp as int), cast(avg_solar as int)
from staging_ufo_count_ca ufo
join staging_wheater_data_aggr aggr on aggr.date_format = ufo.date;