-- Wichtig!!! Vorher HBase starten
-- sudo service hbase-master start
-- sudo service hbase-regionserver start

-- Hive Script um die Metadaten in Hive und HBase abzulegen
-- Tabellen l√∂schen, falls sie existieren und anschliessend neu anlegen und befuellen
DROP TABLE IF EXISTS `hbase_stations`;
DROP TABLE IF EXISTS `hbase_counties`;
DROP TABLE IF EXISTS `hbase_cities_counties`;
DROP TABLE IF EXISTS `stations`;
DROP TABLE IF EXISTS `counties`;
DROP TABLE IF EXISTS `cities_counties`;

-- Extene Tabellen von HDFS in Hive anlegen
-- Table stations
CREATE EXTERNAL TABLE stations(name STRING, description STRING,
     city STRING)
 ROW FORMAT DELIMITED FIELDS TERMINATED BY '\073'
 STORED AS TEXTFILE
 LOCATION '/user/cloudera/Datasources/Meta/Stations'
 TBLPROPERTIES ("skip.header.line.count"="1");

-- Table cities_counties 
CREATE EXTERNAL TABLE cities_counties(city STRING, county STRING)
 ROW FORMAT DELIMITED FIELDS TERMINATED BY '\073'
 STORED AS TEXTFILE
 LOCATION '/user/cloudera/Datasources/Meta/CountiesCities'
 TBLPROPERTIES ("skip.header.line.count"="1");

-- Table counties
CREATE EXTERNAL TABLE counties(county STRING)
 ROW FORMAT DELIMITED FIELDS TERMINATED BY '\073'
 STORED AS TEXTFILE
 LOCATION '/user/cloudera/Datasources/Meta/Counties';

-- Externe HBase Tabellen anlegen
-- Table stations
CREATE TABLE hbase_stations(key string, station string, city string) 
STORED BY 'org.apache.hadoop.hive.hbase.HBaseStorageHandler'
WITH SERDEPROPERTIES ("hbase.columns.mapping" = ":key,st:station,st:city")
TBLPROPERTIES ("hbase.table.name" = "stations");

-- Table cities_counties
CREATE TABLE hbase_cities_counties(key string, city string, county string) 
STORED BY 'org.apache.hadoop.hive.hbase.HBaseStorageHandler'
WITH SERDEPROPERTIES ("hbase.columns.mapping" = ":key,cc:city,cc:county")
TBLPROPERTIES ("hbase.table.name" = "cities_counties");

-- Table counties
CREATE TABLE hbase_counties(key string, county string, name string) 
STORED BY 'org.apache.hadoop.hive.hbase.HBaseStorageHandler'
WITH SERDEPROPERTIES ("hbase.columns.mapping" = ":key,ct:county,ct:name")
TBLPROPERTIES ("hbase.table.name" = "counties");

-- Daten in HBase einfuegen
-- stations
INSERT OVERWRITE TABLE hbase_stations SELECT name as key, name, city  FROM stations;
-- cities_counties
INSERT OVERWRITE TABLE hbase_cities_counties SELECT city as key, city, county  FROM cities_counties;
-- counties
INSERT OVERWRITE TABLE hbase_counties SELECT county as key, county, county FROM counties;
